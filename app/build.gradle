apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "ru.anisart.vv"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 29
        versionName "1.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        debug {}
        release {
            if (project.hasProperty("keys.repo")) {
                def projectPropsFile = file(project.property("keys.repo") + "/vv.properties")
                if (projectPropsFile.exists()) {
                    Properties props = new Properties()
                    props.load(new FileInputStream(projectPropsFile))
                    storeFile file(file(project.property("keys.repo") + props['RELEASE_STORE_FILE']))
                    storePassword props['RELEASE_STORE_PASS']
                    keyAlias props['RELEASE_ALIAS']
                    keyPassword props['RELEASE_KEY_PASS']
                }
            } else {
                println "======================================================="
                println "[ERROR] - Please configure release-compilation environment - e.g. in ~/.signing directory"
                println "======================================================="
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a', 'armeabi'//select ABIs to build APKs for
            universalApk false //generate an additional APK that contains all the ABIs
        }
    }
    // map for the version code
    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    android.defaultConfig.versionCode * 10000000 +
                            project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0)
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile("com.github.hotchemi:permissionsdispatcher:$permission_dispatcher_version") {
        // if you don't use android.app.Fragment you can exclude support for them
        exclude module: "support-v13"
    }
    kapt "com.github.hotchemi:permissionsdispatcher-processor:$permission_dispatcher_version"
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile 'com.github.dmytrodanylyk.android-process-button:library:1.0.4'
    compile "com.jakewharton:butterknife:8.8.1"
    kapt "com.jakewharton:butterknife-compiler:8.8.1"
    compile ('com.github.codekidX:storage-chooser:2.0.2') {
        exclude group: 'com.android.support'
    }
    compile('com.mapbox.mapboxsdk:mapbox-android-sdk:5.1.3@aar') {
        transitive = true
        exclude group: 'com.android.support'
    }
    compile 'com.mapbox.mapboxsdk:mapbox-android-services:2.2.3@aar'
    compile 'com.mapbox.mapboxsdk:mapbox-java-services-rx:2.2.3'
//    compile 'com.mapbox.mapboxsdk:mapbox-android-directions:1.0.0@aar'
//    compile 'com.esri.geometry:esri-geometry-api:1.2.1'
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    compile 'com.jaredrummler:colorpicker:1.0.1'
    compile 'com.github.clans:fab:1.6.4'
    compile "org.jetbrains.anko:anko-commons:0.10.1"
    compile 'com.patloew.rxlocation:rxlocation:1.0.3'
}
repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.google.com' }
}
